sp {clean-house*propose*re-record-object
   (state <s> ^name robot
              ^current-location <ca>
              ^objects.object <block>
           #   ^parameters.objects-held-in smem
              ^io.input-link.objects.object <obj>)
   (<block> ^id <id>
            ^area <> <ca>)
   (<obj> ^id <id>
          ^visible true)
-->
   (<s> ^operator <op> + =, >)
   (<op> ^name re-record-object
         ^object <block>)
}

sp {clean-house*propose*re-record-object*storage
   (state <s> ^name robot
              ^current-location <ca>
              ^objects.object <block>)
           #   ^parameters.objects-held-in smem)
   (<block> ^area <ca>
            ^in-storage-room no)
   (<ca> ^storage-area yes)
-->
   (<s> ^operator <op> + =, >)
   (<op> ^name re-record-object
         ^object <block>)
}

sp {apply*re-recorded-block*storage
   (state <s> ^operator <op>
              ^current-location.storage-area yes)
   (<op> ^name re-record-object
         ^object <block>)
   (<block> ^in-storage-room no)
-->
   (<block> ^in-storage-room no - yes)
}

sp {apply*re-recorded-block*area
   (state <s> ^operator <op>
              ^current-location <ca>)
   (<op> ^name re-record-object
         ^object <block>)
   (<block> ^area { <area> <> <ca> })
-->
   (<block> ^area <area> - <ca>)
}

sp {apply*re-record-object*store-new
   (state <s> ^operator <op>
              ^smem.command <cmd>)
   (<op> ^name re-record-object
         ^object <block>)
-->
   (write (crlf) |Re-record block | <block> )
   (<cmd> ^store <block>)
}

sp {clean-house*compare*re-record-object
   (state <s> ^name robot
              ^operator <op1> +
                        <op2> +)
   (<op1> ^name re-record-object)
   (<op2> ^name get-object)
-->
   (<s> ^operator <op1> > <op2>)
}
