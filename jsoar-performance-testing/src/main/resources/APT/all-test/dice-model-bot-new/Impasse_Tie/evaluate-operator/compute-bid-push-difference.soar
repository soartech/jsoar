## Could combine with compute-bid-difference if that used new unknown computation.

## Only evaluate after other bids (bid with no pass, challenge)

sp {evaluate-operator*propose*compute-bid-push-difference
   (state <s> ^name evaluate-operator
             -^estimated-total
              ^top-state.probabilities false
              ^superstate.operator.superoperator <so>)
   (<so> ^name bid
         ^push-face-count
         ^face <face>
         ^multiplier <multipler>)
-->
   (<s> ^operator <op> + =)
   (<op> ^name compute-bid-push-difference
         ^face <face>
         ^multiplier <multipler>)
}

##################################
sp {apply*compute-bid-push-difference*1*bid
   (state <s> ^operator <op>
              ^dice-counts.dice-count <dc1>)
   (<op> ^name compute-bid-push-difference
         ^face 1
         ^multiplier <bid-multi>
         ^unknown <multi-unknown>)
   (<dc1> ^face 1
          ^count <multi-1>)
-->
   (write (crlf) |  Bid: | <bid-multi> | 1's. Estimated total: | (+ (/ <multi-unknown> 6) <multi-1>) | Bid Difference: | (- (+ (/ <multi-unknown> 6) <multi-1>) <bid-multi>))
   (<s>  ^total-known (- <multi-1> <bid-multi>)
         ^total-possible (- (+ <multi-unknown> <multi-1>) <bid-multi>)
         ^estimated-total (+ (/ <multi-unknown> 6) <multi-1>)
         ^bid-difference (float (- (+ (/ <multi-unknown> 6) <multi-1>) <bid-multi>)))
}

sp {apply*compute-bid-push-difference*special-rules*bid
   (state <s> ^operator <op>
              ^top-state.io.input-link.state.special true
              ^dice-counts.dice-count <dc1>)
   (<op> ^name compute-bid-push-difference
         ^face { <> 1 <face> }
         ^multiplier <bid-multi>
         ^unknown <multi-unknown>)
   (<dc1> ^face <face>
          ^count <multi-1>)
-->
   (write (crlf) |  Bid: | <bid-multi> | | <face> |'s. Estimated total: | (+ (/ <multi-unknown> 6) <multi-1>) | Bid Difference: | (- (+ (/ <multi-unknown> 6) <multi-1>) <bid-multi>) | special rules|)
   (write (crlf) |    Unknowns: | <multi-unknown> | Showing: | <multi-1>)
   (<s> ^total-known (- <multi-1> <bid-multi>)
        ^total-possible (- (+ <multi-unknown> <multi-1>) <bid-multi>)
        ^estimated-total (+ (/ <multi-unknown> 6) <multi-1>)
        ^bid-difference (float (- (+ (/ <multi-unknown> 6) <multi-1>) <bid-multi>)))
}

sp {apply*compute-bid-push-difference*not-special-rules*bid
   (state <s> ^operator <op>
              ^top-state.io.input-link.state.special false
              ^dice-counts.dice-count <dc1> <dc2>)
   (<op> ^name compute-bid-push-difference
         ^face { <> 1 <face> }
         ^multiplier <bid-multi>
         ^unknown <multi-unknown>)
   (<dc1> ^face <face>
          ^count <multi-bid>)
   (<dc2> ^face 1
          ^count <multi-1>)
-->
   (write (crlf) |  Bid: | <bid-multi> | | <face> |'s. Estimated total: | (+ (/ <multi-unknown> 3) <multi-1> <multi-bid>) | Bid Difference: | (- (+ (/ <multi-unknown> 3) <multi-1> <multi-bid>) <bid-multi>) | normal rules|)
   (write (crlf) |    Unknowns: | <multi-unknown> | Showing: | <multi-1>)
   (<s> ^total-known (- (+ <multi-1> <multi-bid> ) <bid-multi>)
        ^total-possible (- (+ <multi-unknown> <multi-bid> <multi-1>) <bid-multi>)
        ^estimated-total (+ (/ <multi-unknown> 3) <multi-bid> <multi-1>)
        ^bid-difference (float (- (+ (/ <multi-unknown> 3) <multi-bid> <multi-1>) <bid-multi>)))
}

